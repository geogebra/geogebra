
Function("Append",{list,element})
[
  Check(IsList(list), "Argument", "The first argument must be a list.");

  Insert(list,Length(list)+1,element);
];



LocalSymbols(max,f,result)
[
  BSearch(max,f)  :=
  [
    Local(result);
    Bind(result, FindIsq(max,f));
    If(Apply(f,{result})!=0,Bind(result,-1));
    result;
  ];
];
UnFence("BSearch",2);


Function("BubbleSort",{list,compare})
[
  Local(i,j,length,left,right);

  list:=FlatCopy(list);
  length:=Length(list);

  For (j:=length,j>1,j--)
  [
    For(i:=1,i<j,i++)
    [
      left:=list[i];
      right:=list[i+1];
      If(Not(Apply(compare,{left,right})),
        [
          DestructiveInsert(DestructiveDelete(list,i),i+1,left);
        ]
      );
    ];
  ];
  list;
];


Function("Contains",{list,element})
[
  Local(result);
  Bind(result,False);
  While(And(Not(result), Not(IsEqual(list, {}))))
  [
    If(IsEqual(First(list),element),
      Bind(result, True),
      Bind(list, Rest(list))
      );
  ];
  result;
];


Function("Count",{list,element})
[
   Local(result);
   Bind(result,0);
   ForEach(item,list) If(IsEqual(item, element), Bind(result,AddN(result,1)));
   result;
];


Function("DestructiveAppend",{list,element})
[
  DestructiveInsert(list,Length(list)+1,element);
];


Function("DestructiveAppendList",{list,toadd})
[
  Local(i,nr);
  nr:=Length(toadd);
  For(i:=1,i<=nr,i++)
  [
    DestructiveAppend(list,toadd[i]);
  ];
  True;
];


Function("Difference",{list1,list2})
[
  Local(l2,index,result);
  l2:=FlatCopy(list2);
  result:=FlatCopy(list1);
  ForEach(item,list1)
  [
    Bind(index,Find(l2,item));
    If(index>0,
      [
        DestructiveDelete(l2,index);
        DestructiveDelete(result,Find(result,item));
      ]
      );
  ];
  result;
];








Rulebase("Drop", {lst, range});

Rule("Drop", 2, 1, IsList(range))
    Concat(Take(lst,range[1]-1), Drop(lst, range[2]));

Rule("Drop", 2, 2, range >= 0)
    If( range = 0 Or lst = {}, lst, Drop( Rest(lst), range-1 ));

Rule("Drop", 2, 2, range < 0)
    Take( lst, Length(lst) + range );


Function("FillList", {aItem, aLength})
[
  Local(i, aResult);
  aResult:={};
  For(i:=0, i<aLength, i++)
    DestructiveInsert(aResult,1,aItem);
  aResult;
];


Function("Find",{list,element})
[
  Local(result,count);
  Bind(result, -1);
  Bind(count, 1);
  While(And(result<0, Not(IsEqual(list, {}))))
  [
    If(IsEqual(First(list), element),
      Bind(result, count)
      );
    Bind(list,Rest(list));
    Bind(count,AddN(count,1));
  ];
  result;
];


LocalSymbols(max,f,low,high,mid,current)
[
FindIsq(max,f)  :=
[
  Local(low,high,mid,current);
  low:=1;
  high:=max+1;
  Bind(mid,((high+low)>>1));
  While(high>low And mid>1)
  [
    Bind(mid,((high+low)>>1));
    Bind(current,Apply(f,{mid}));

    If(current = 0,
       high:=low-1,
       If(current > 0,
          Bind(high,mid),
          Bind(low,mid+1)
          )
       );
  ];
  mid;
];
];
UnFence("FindIsq",2);



Function("FindPredicate",{list,predicate})
[
  Local(result,count);
  Bind(result, -1);
  Bind(count, 1);
  While(And(result<0, Not(IsEqual(list, {}))))
  [
    If(Apply(predicate,{First(list)}),
      Bind(result, count)
      );
    Bind(list,Rest(list));
    Bind(count,AddN(count,1));
  ];
  result;
]; 







10 # FuncList(expr_IsAtom) <-- {};
20 # FuncList(expr_IsFunction) <--
RemoveDuplicates(
	Concat(
		{First(FunctionToList(expr))},
		Apply("Concat",
			MapSingle("FuncList", Rest(FunctionToList(expr)))
		)
	)
);


10 # FuncList(expr_IsAtom, look'list_IsList) <-- {};

20 # FuncList(expr_IsFunction, look'list_IsList)_(Not Contains(look'list, ToAtom(Type(expr)))) <-- {ToAtom(Type(expr))};

30 # FuncList(expr_IsFunction, look'list_IsList) <--
RemoveDuplicates(
	Concat(
		{First(FunctionToList(expr))},
		[	
			
			Local(item, result);
			result := {};
			ForEach(item, expr) result := Concat(result, FuncList(item, look'list));
			result;
		]
	)
);

HoldArgumentNumber("FuncList", 1, 1);
HoldArgumentNumber("FuncList", 2, 1);




FuncListArith(expr) := FuncList(expr, {ToAtom("+"), ToAtom("-"), *, /});

HoldArgumentNumber("FuncListArith", 1, 1);





HeapSort(list, compare) := HeapSort(list, ArrayCreate(Length(list), 0), 1, Length(list), compare);


1 # HeapSort(_list, _tmplist, _first, _last, _compare) _ (last - first <= 2) <-- SmallSort(list, first, last, compare);
2 # HeapSort(_list, _tmplist, _first, _last, _compare) <--
[	
	
	
	Local(mid, ileft, iright, pleft);
	mid := first+((last-first)>>1);
	HeapSort(list, tmplist, first, mid, compare);
	HeapSort(list, tmplist, mid+1, last, compare);
	
	For(ileft := first,  ileft <= mid, ileft++)
		tmplist[ileft] := list[ileft];
	For(
		[ileft := first; pleft := first; iright := mid+1;],
		ileft <= mid,	
		pleft++	
	)	
		
		
		
	If(	
		iright>last Or Apply(compare,{tmplist[ileft],list[iright]}),
		[	
			list[pleft] := tmplist[ileft];
			ileft++;
		],
		[	
			list[pleft] := list[iright];
			iright++;
		]
	);

	list;
];




10 # Intersection( LoL_IsList )_(AllSatisfy("IsList",LoL)) <--
[
  
  Local(nLists,L0,L1,ii,result,LI);
  nLists := Length(LoL);
  
  If( nLists = 1,
    [
        result := LoL[1];
    ],
    [
      L0 := FlatCopy(LoL[1]);
      For( ii:=2,ii<=nLists,ii++)
      [
          L1 := FlatCopy(LoL[ii]);
          
          LI := Intersection(L0,L1);
          
          L0 := FlatCopy(LI);
      ];
      
      result := L0;
    ]
  );
  result;
];
  

11 # Intersection(list1_IsList,list2_IsList) <--
[
    
    Local(l2,index,result);
    l2:=FlatCopy(list2);
    result:={};
    ForEach(item,list1)
    [
        Bind(index, Find(l2,item));
        If(index>0,
          [
             DestructiveDelete(l2,index);
             DestructiveInsert(result,1,item);
          ]
        );
    ];
    DestructiveReverse(result);
];




Macro("MacroMapArgs",{expr,oper})
[
  Local(ex,tl,op);
  Bind(op,@oper);
  Bind(ex,FunctionToList(@expr));
  Bind(tl,Rest(ex));

   ListToFunction(Concat({ex[1]},
     `MacroMapSingle(@op,Hold(@tl)))
   );
];

UnFence("MacroMapArgs",2);
HoldArgument("MacroMapArgs",oper);



TemplateFunction("MacroMapSingle",{func,list})
[
  Local(mapsingleresult);
  mapsingleresult:={};

  ForEach(mapsingleitem,list)
  [
    DestructiveInsert(mapsingleresult,1,
      `ApplyFast(func,{Hold(Hold(@mapsingleitem))}));
  ];
  DestructiveReverse(mapsingleresult);
];
UnFence("MacroMapSingle",2);
HoldArgument("MacroMapSingle",func);
HoldArgument("MacroMapSingle",list);


LocalSymbols(func,lists,mapsingleresult,mapsingleitem)
[
  TemplateFunction("Map",{func,lists})
  [
    Local(mapsingleresult,mapsingleitem);
    mapsingleresult:={};
    lists:=Transpose(lists);
    ForEach(mapsingleitem,lists)
    [
      DestructiveInsert(mapsingleresult,1,Apply(func,mapsingleitem));
    ];
    DestructiveReverse(mapsingleresult);
  ];
  UnFence("Map",2);
  HoldArgument("Map",func);
];


TemplateFunction("MapArgs",{expr,oper})
[
  Bind(expr,FunctionToList(expr));
   ListToFunction(Concat({expr[1]},
     Apply("MapSingle",{oper,Rest(expr)})
   ) );
];
UnFence("MapArgs",2);
HoldArgument("MapArgs",oper);


TemplateFunction("MapSingle",{func,list})
[
  Local(mapsingleresult);
  mapsingleresult:={};

  ForEach(mapsingleitem,list)
  [
    DestructiveInsert(mapsingleresult,1,
      Apply(func,{mapsingleitem}));
  ];
  DestructiveReverse(mapsingleresult);
];
UnFence("MapSingle",2);
HoldArgument("MapSingle",func);






Partition(lst, len):=
	If( Length(lst) < len Or len = 0, {},
        	Concat( {Take(lst,len)}, Partition(Drop(lst,len), len) ));


Function("Pop",{stack,index})
[
  Local(result);
  result:=stack[index];
  DestructiveDelete(stack,index);
  result;
];


Function("PopBack",{stack})  Pop(stack,Length(stack));


Function("PopFront",{stack}) Pop(stack,1);






10 # PrintList(list_IsList) <-- PrintList(list, ", ");
10 # PrintList({}, padding_IsString) <-- "";
20 # PrintList(list_IsList, padding_IsString) <-- PipeToString() [
	Local(i);
	ForEach(i, list) [
		If(Not(IsEqual(i, First(list))), WriteString(padding));
		If (IsString(i), WriteString(i), If(IsList(i), WriteString("{" : PrintList(i, padding) : "}"), Write(i)));
	];
];


Function("Push",{stack,element})
[
  DestructiveInsert(stack,1,element);
];


Remove(list, expression) :=
[
   Local(result);
   Bind(result,{});
   ForEach(item,list)
   If(item != expression, DestructiveAppend(result,item));
   result;
];


Function("RemoveDuplicates",{list})
[
   Local(result);
   Bind(result,{});
   ForEach(item,list)
     If(Not(Contains(result,item)),DestructiveAppend(result,item));
   result;
];



Reverse(list):=DestructiveReverse(FlatCopy(list));


LocalSymbols(predicate,list,result,item)
[
  Function("Select",{list,predicate})
  [
    Local(result);
    result:={};
    ForEach(item,list)
    [
      If(Apply(predicate,{item}),DestructiveAppend(result,item));
    ];
    result;
  ];
  HoldArgument("Select",predicate);
  UnFence("Select",2);
];




SmallSort(_list, _first, _last, _compare) _ (last=first) <-- list;
SmallSort(_list, _first, _last, _compare) _ (last=first+1) <--
[
	Local(temp);
	temp := list[first];
	If(
		Apply(compare,{temp,list[last]}),
		list,
		[
			list[first] := list[last];
			list[last] := temp;
		]	
	);
	list;
];
SmallSort(_list, _first, _last, _compare) _ (last=first+2) <--
[
	Local(temp);
	temp := list[first];
	If(
		Apply(compare,{list[first+1],temp}),
		[
			list[first] := list[first+1];
			list[first+1] := temp;
		]	
	);
	
	temp := list[last];
	If(
		Apply(compare,{list[first],temp}),
		If(	
			Apply(compare,{list[first+1],temp}),
			list,
			[
				list[last] := list[first+1];
				list[first+1] := temp;
			]	
		),
		[	
			list[last] := list[first+1];
			list[first+1] := list[first];
			list[first] := temp;
		]
	);
	list;
];


Function("Swap",{list,index1,index2})
[
  Local(item1,item2);
  item1:=list[index1];
  item2:=list[index2];
  list[index1] := item2;
  list[index2] := item1;
];




TemplateFunction("Table",{body,var,count'from,count'to,step})
  [
    MacroLocal(var);
    Local(result,nr,ii);
    result:={};
    nr := (count'to - count'from) / step;
    ii := 0;
    While( ii <= nr )
      [
       MacroBind( var, count'from + ii * step );
       DestructiveInsert( result,1,Eval(body) );
       Bind(ii,AddN(ii,1));
      ];
    DestructiveReverse(result);
  ];
HoldArgumentNumber("Table",5,1); 
HoldArgumentNumber("Table",5,2); 
UnFence("Table",5);








Rulebase("Take", {lst, range});

Rule("Take", 2, 1, IsList(range))
    Take( Drop(lst, range[1] -1), range[2] - range[1] + 1);

Rule("Take", 2, 2, range >= 0)
    If( Length(lst)=0 Or range=0, {},
        Concat({First(lst)}, Take(Rest(lst), range-1)));

Rule("Take", 2, 2, range < 0)
    Drop( lst, Length(lst) + range );


Function("Union",{list1,list2})
[
  RemoveDuplicates(Concat(list1,list2));
];



VarList(_expr) <-- VarList(expr,"IsVariable");

Function("VarList",{expr,filter})
[
  RemoveDuplicates(VarListAll(expr,filter));
];





VarListAll(_expr) <-- VarListAll(expr,"IsVariable");

10 # VarListAll(_expr,_filter)_(Apply(filter,{expr}) = True) <--
     {expr};


20 # VarListAll(expr_IsFunction,_filter) <--
[
  Local(item,result, flatlist);
  Bind(flatlist,Rest(FunctionToList(expr)));
  Bind(result,{});
  ForEach(item,flatlist)
    Bind(result,Concat(result,VarListAll(item,filter)));
  result;
];


30 # VarListAll(_expr,_filter) <-- {};




VarListArith(expr) := VarListSome(expr, {ToAtom("+"), ToAtom("-"), *, /});




10 # VarListSome({}, _look'list) <-- {};

10 # VarListSome(expr_IsVariable, _look'list) <-- {expr};
15 # VarListSome(expr_IsAtom, _look'list) <-- {};

20 # VarListSome(expr_IsFunction, look'list_IsList)_(Not Contains(look'list, ToAtom(Type(expr)))) <-- {expr};

30 # VarListSome(expr_IsFunction, look'list_IsList) <--
RemoveDuplicates(
		[	
			Local(item, result);
			result := {};
			ForEach(item, expr) result := Concat(result, VarListSome(item, look'list));
			result;
		]
);







LocalSymbols(GlobalStack, x)
[
  GlobalStack := {};

	GlobalPop(x_IsAtom) <--
	[
		Check(Length(GlobalStack)>0, "Invariant", "GlobalPop: Error: empty GlobalStack");
		MacroBind(x, PopFront(GlobalStack));
		Eval(x);
	];

	HoldArgumentNumber("GlobalPop", 1, 1);

	GlobalPop() <--
	[
		Check(Length(GlobalStack)>0, "Invariant", "GlobalPop: Error: empty GlobalStack");
		PopFront(GlobalStack);
	];

	GlobalPush(_x) <--
	[
		Push(GlobalStack, x);
		x;
	];
];

