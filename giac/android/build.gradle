buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.1'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.+'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }

}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.library'

repositories {
    mavenCentral()
    jcenter()
}

android {
    compileSdkVersion 23
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 23
    }
}

def jniLibsDir = "src/main/jniLibs"
def armeabiDir = "$jniLibsDir/armeabi-v7a"
def x86Dir = "$jniLibsDir/x86"

task copyCrystaxSo {
    description 'Copies Crystax .so files to the appropriate folders.'
    def found = 0
    doLast {
	def so = "libcrystax.so"
	def PATH = System.env['PATH'].split(":").each { d ->
	    def libdirArmeabi = d + "/../arm-linux-androideabi/lib/"
	    def libdirX86 = d + "/../i686-linux-android/lib/"
	    if (file(libdirArmeabi + so).exists()) {
		println " Found armeabi .so"
		found ++
		copy { from libdirArmeabi include so into armeabiDir }
		}
	    if (file(libdirX86 + so).exists()) {
		println " Found x86 .so"
		found ++
		copy { from libdirX86 include so into x86Dir }
		}
	    }
	if (found < 2) {
	    def missing = 2 - found
	    throw new GradleException("Missing $missing $so libraries, check PATH")
	    }
	}
    }

// Include Gradle code for publishing (not available in GPL'ed version):
def publishScript = file('../../deploy/giac-android.gradle')
if (publishScript.exists()) {
    apply from: publishScript
    }
