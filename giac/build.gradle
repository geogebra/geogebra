// Authors: Balazs Bencze <balazs.bencze@geogebra.org>,
// and Zoltan Kovacs <zoltan@geogebra.org>.
// Based on Bernard Parisse's original Makefiles and other scripts.

project.setDescription('Giac CAS for GeoGebra')

apply from: 'gradle-scripts/repositories.gradle'

apply plugin: 'cpp'
apply plugin: 'visual-studio'

// FIXME: These seetings are hardcoded at the moment, they need to be more general.
// Set this as needed:
def externalSourceDir = "/home/autotest/gmp+mpfr-llvm"
// You may need to download and unzip following packages:
def gmpSourceDir = "$externalSourceDir/gmp-6.0.0"
def mpfrSourceDir = "$externalSourceDir/mpfr-3.1.2"
def emgiacSourceDir = "$externalSourceDir/emgiac"

def java_home = org.gradle.internal.jvm.Jvm.current().javaHome

def emccCommand = "emcc"
// You may override it by using environmental variable:
if (System.env['EMCC'] != null) {
    emccCommand = System.env['EMCC']
}
// Also consider setting LLVM for the older emscripten version.
// Emscripten should be configured before running this script, and on the first run there may be problems.

def jsPrebuiltDir = "src/giac.js/prebuilt"
def LlvmMpfrA = "$jsPrebuiltDir/libmpfr.a"
def LlvmGmpA = "$jsPrebuiltDir/libgmp.a"
if (System.env['EXT_GMPMPFR'] != null && System.env['EXT_GMPMPFR'] == "local") {
    LlvmMpfrA = "$mpfrSourceDir/src/.libs/libmpfr.a"
    LlvmGmpA = "$gmpSourceDir/.libs/libgmp.a"
}
if (System.env['EXT_GMPMPFR'] != null && System.env['EXT_GMPMPFR'] == "emgiac") {
    LlvmMpfrA = "$emgiacSourceDir/giac/libmpfr.a"
    LlvmGmpA = "$emgiacSourceDir/giac/libgmp.a"
}
def emscriptenVer = '201304'
if (System.env['EMSCRIPTEN_VER'] != null && System.env['EMSCRIPTEN_VER'] == "201505") {
    emscriptenVer = '201505'
}

def emsdkUrlFolder = 'https://s3.amazonaws.com/mozilla-games/emscripten/releases'
def emsdkTgzFile = 'emsdk-portable.tar.gz'
def emsdkDir = file('build/emsdk')
def emsdkRunDir = file("$emsdkDir/emsdk_portable")
def emscriptenDir = file("$emsdkRunDir/emscripten/1.30.0")

import org.apache.tools.ant.taskdefs.condition.Os

def exec_(String... script) {
    if (!Os.isFamily(Os.FAMILY_MAC)) {
        return ""
    }
    def retVal
    exec {
        commandLine script
        standardOutput = new ByteArrayOutputStream()
        retVal = {
            standardOutput.toString().trim()
        }
    }
    return retVal()
}

def gccBin = exec_("xcrun", "--sdk", "macosx", "--find", "gcc")
def gccPath = "/usr/bin"
if (gccBin.length() > 0) {
    gccPath = gccBin.substring(0, gccBin.lastIndexOf("/"))
}
def clangBin = exec_("xcrun", "--sdk", "iphoneos", "--find", "clang")
def clangPath = "/usr/bin"
if (clangBin.length() > 0) {
    clangPath = clangBin.substring(0, clangBin.lastIndexOf("/"))
}
def iphoneosSdk = exec_("xcrun", "--sdk", "iphoneos", "--show-sdk-path")
def iphonesimulatorSdk = exec_("xcrun", "--sdk", "iphonesimulator", "--show-sdk-path")
def iphoneosPlatform = exec_("xcrun", "--sdk", "iphoneos", "--show-sdk-platform-path")
def minIosVersion = '9.0'

def iosClangCompilerArgs(args, sdk, arch, minIosVersion) {
    args << "-isysroot"
    args << "${sdk}"
    args << "-arch"
    args << "${arch}"
    args << "-miphoneos-version-min=${minIosVersion}"
    args << "-std=gnu++98"
    args << "-stdlib=libc++"
    args << "-fembed-bitcode"
    args << "-O0"
}

def giacCommonDefines(cppCompiler) {
    cppCompiler.define "GIAC_GGB"
    cppCompiler.define "IN_GIAC"
    cppCompiler.define "GIAC_GENERIC_CONSTANTS"
    cppCompiler.define "HAVE_UNISTD_H"
    cppCompiler.define "HAVE_LIBPTHREAD"
    cppCompiler.define "HAVE_SYSCONF"
    cppCompiler.define "HAVE_NO_HOME_DIRECTORY"
    cppCompiler.define "VERSION", '"1.2.3"'
    cppCompiler.define "TIMEOUT"
    cppCompiler.define 'HAVE_SYS_TIMES_H'
    cppCompiler.define 'HAVE_SYS_TIME_H'
}

def giacIosDefines(cppCompiler) {
    cppCompiler.define 'APPLE_SMART'
    cppCompiler.define 'NO_GETTEXT'
    cppCompiler.define 'NO_SCANDIR'
    cppCompiler.define 'OSX_10_9_CXX'
    cppCompiler.define 'HAVE_CONFIG_H'
}

model {
    repositories {
        libs(PrebuiltLibraries) {
            mpfr {
                binaries.withType(StaticLibraryBinary) {
                    def arch = targetPlatform.architecture.name
                    def os = targetPlatform.operatingSystem.name
                    staticLibraryFile = file("src/jni/prebuilt/$os/$arch/libmpfr.a")
                }
            }
            gmp {
                binaries.withType(StaticLibraryBinary) {
                    def arch = targetPlatform.architecture.name
                    def os = targetPlatform.operatingSystem.name
                    staticLibraryFile = file("src/jni/prebuilt/$os/$arch/libgmp.a")
                }
            }
        }
    }

    platforms {
        win32 {
            architecture 'i386'
            operatingSystem 'windows'
        }
        win64 {
            architecture 'x64'
            operatingSystem 'windows'
        }
        linux32 { // this will be fixed in Gradle 2.4 (-> linux32)
            architecture 'i386'
            operatingSystem 'linux'
        }
        linux64 {
            architecture 'x64'
            operatingSystem 'linux'
        }
        osx_amd64 {
            architecture 'amd64'
            operatingSystem 'osx'
        }
        androideabi {
            architecture 'arm'
            operatingSystem 'android'
        }
        androidx86 {
            architecture 'x86'
            operatingSystem 'android'
        }
        ios_armv7 {
            architecture 'armv7'
            operatingSystem 'ios'
        }
        ios_arm64 {
            architecture 'arm64'
            operatingSystem 'ios'
        }
        ios_i386 {
            architecture 'i386'
            operatingSystem 'ios'
        }
        ios_x86_64 {
            architecture 'x86_64'
            operatingSystem 'ios'
        }
    }

    toolChains {
        mingw(Gcc) {
            target('win32') {
                cppCompiler.executable 'i686-w64-mingw32-g++'
                linker.executable 'i686-w64-mingw32-g++'
            }
            target('win64') {
                cppCompiler.executable 'x86_64-w64-mingw32-g++'
                linker.executable 'x86_64-w64-mingw32-g++'
            }
        }
        clang(Clang) {
            target('osx_amd64') {
                cppCompiler.executable 'clang'
                linker.executable 'gcc'
                path gccPath, clangPath
            }
            target('ios_armv7') {
                cppCompiler.executable 'clang'
                cppCompiler.withArguments { args ->
                    iosClangCompilerArgs(args, iphoneosSdk, "armv7", minIosVersion)
                }
                path clangPath
            }
            target('ios_arm64') {
                cppCompiler.executable 'clang'
                cppCompiler.withArguments { args ->
                    iosClangCompilerArgs(args, iphoneosSdk, "arm64", minIosVersion)
                }
                path clangPath
            }
            target('ios_i386') {
                cppCompiler.executable 'clang'
                cppCompiler.withArguments { args ->
                    iosClangCompilerArgs(args, iphonesimulatorSdk, "i386", minIosVersion)
                }
                path clangPath
            }
            target('ios_x86_64') {
                cppCompiler.executable 'clang'
                cppCompiler.withArguments { args ->
                    iosClangCompilerArgs(args, iphonesimulatorSdk, "x86_64", minIosVersion)
                }
                path clangPath
            }
        }
        // Note: Make sure manually that these executables are on the PATH.
        android(Gcc) {
            target('androideabi') {
                cppCompiler.executable 'arm-linux-androideabi-g++'
                linker.executable 'arm-linux-androideabi-g++'
            }
            target('androidx86') {
                cppCompiler.executable 'i686-linux-android-g++'
                linker.executable 'i686-linux-android-g++'
            }
        }
    }

    components {
        // giac static libary
        giac(NativeLibrarySpec) {
            targetPlatform 'linux32'
            targetPlatform 'linux64'
            targetPlatform 'ios_armv7'
            targetPlatform 'ios_arm64'
            targetPlatform 'ios_i386'
            targetPlatform 'ios_x86_64'

            binaries.all {
                giacCommonDefines(cppCompiler)
                if (targetPlatform.operatingSystem.name == 'ios') {
                    giacIosDefines(cppCompiler)
                    if (targetPlatform.architecture.name == 'arm-v8') {
                        cppCompiler.define 'x86_64'
                    }
                }
            cppCompiler.args '-fpermissive' // needed for recent G++

            }
        }

        simpleInterface(NativeLibrarySpec) {
            targetPlatform 'ios_armv7'
            targetPlatform 'ios_arm64'
            targetPlatform 'ios_i386'
            targetPlatform 'ios_x86_64'

            binaries.all {
                lib library: 'giac', linkage: 'static'
                giacCommonDefines(cppCompiler)
                if (targetPlatform.operatingSystem.name == 'ios') {
                    giacIosDefines(cppCompiler)
                    if (targetPlatform.architecture.name == 'arm-v8') {
                        cppCompiler.define 'x86_64'
                    }
                }
            }
        }

        minigiac(NativeExecutableSpec) {
            targetPlatform 'linux32'
            targetPlatform 'linux64'

            binaries.all {
                lib library: 'giac', linkage: 'static'
                linker.args '-lgmp', '-lmpfr', '-lpthread'
                cppCompiler.define 'GIAC_GGB'
                cppCompiler.define 'IN_GIAC'
                cppCompiler.define 'GIAC_GENERIC_CONSTANTS'
                cppCompiler.define 'HAVE_CONFIG_H'
                cppCompiler.define 'HAVE_UNISTD_H'
                cppCompiler.define 'HAVE_SYS_TIMES_H'
                cppCompiler.define 'HAVE_SYS_TIME_H'
                cppCompiler.args '-fpermissive' // needed for recent G++
            }
            sources.all {
                source {
                    srcDirs 'src/giac/cpp', 'src/minigiac/cpp'
                }
            }
        }

        javagiac(NativeLibrarySpec) {
            targetPlatform 'win32'
            targetPlatform 'win64'
            targetPlatform 'linux32' // this will be fixed in Gradle 2.4 (-> linux32)
            targetPlatform 'linux64'
            targetPlatform 'osx_amd64'
            targetPlatform 'androideabi'
            targetPlatform 'androidx86'

            sources.cpp {
                source {
                    srcDirs 'src/giac/cpp', 'src/jni/cpp'
                }
                exportedHeaders {
                    srcDirs 'src/giac/headers', 'src/jni/jdkHeaders'
                }
                lib library: 'mpfr', linkage: 'static'
                lib library: 'gmp', linkage: 'static'
            }

            binaries.withType(SharedLibraryBinarySpec) {
                // Common settings for all platforms and architectures:
                cppCompiler.define 'IN_GIAC'
                cppCompiler.define 'GIAC_GENERIC_CONSTANTS'
                // Additional settings can be defined in config.h.
                // They are automatically loaded if HAVE_CONFIG_H is defined below:
                cppCompiler.define 'HAVE_CONFIG_H'
                cppCompiler.define 'GIAC_GGB'

                cppCompiler.args '-fexceptions'
                // cppCompiler.args '-Os' // size optimization
                cppCompiler.args '-O2' // standard optimization (default)
                cppCompiler.args '-I.'

                linker.args "-I${java_home}/include", "-Isrc/jni/jdkHeaders"

                // Architecture based settings:
                if (targetPlatform.architecture.name == 'i386') {
                    cppCompiler.define 'SMARTPTR64'
                    cppCompiler.define 'SIZEOF_LONG', '8'
                } else {
                    cppCompiler.define 'SIZEOF_LONG', '4'
                }

                // OS based settings:
                if (targetPlatform.operatingSystem.name == 'windows') {
                    cppCompiler.define 'GIAC_MPQS'
                    cppCompiler.define '__MINGW_H'
                    cppCompiler.define 'HAVE_NO_SYS_TIMES_H'
                    cppCompiler.define 'HAVE_NO_SYS_RESOURCE_WAIT_H'
                    cppCompiler.define 'HAVE_NO_PWD_H'
                    cppCompiler.define 'HAVE_NO_CWD'
                    cppCompiler.define 'NO_CLOCK'
                    cppCompiler.define 'usleep',''
                    cppCompiler.define 'YY_NO_UNISTD_H'

                    // if (targetPlatform.architecture.name == 'x86') {
                         // This can be done to reduce size, but not necessary:
                         // cppCompiler.args '-UHAVE_PTHREAD_H'
                         // This may help in allowing some deprecated constructs:
                         // cppCompiler.args '-fpermissive'
                         // }

                    cppCompiler.args '-I', file('src/jni/jdkHeaders/win32').toString()
                    // Insert prebuilt libraries
                    linker.args '-Wl,--add-stdcall-alias'
                    linker.args '-s' // stripping

                    // Add libgcc and libstdc++ statically
                    linker.args '-static-libgcc'
                    linker.args '-static-libstdc++'
                    // Statically link libpthread
                    // linker.args '-Wl,-Bstatic', '-lstdc++', '-lpthread'
                    // Or even better, everything
                    linker.args '-static'
                }

                if (targetPlatform.operatingSystem.name == 'linux') {
                    cppCompiler.define 'HAVE_UNISTD_H'

                    cppCompiler.args '-I', file('src/jni/jdkHeaders/linux').toString()
                    cppCompiler.args '-fno-strict-aliasing' // maybe not needed
                    cppCompiler.args '-DPIC' // maybe not needed

                    linker.args '-s' // stripping

                    // Add libgcc and libstdc++ statically
                    linker.args '-static-libgcc'
                    linker.args '-static-libstdc++'

                }
                if (targetPlatform.operatingSystem.name == 'android') {
                    cppCompiler.define 'HAVE_UNISTD_H'
                    cppCompiler.define 'NO_BSD'

                    cppCompiler.args '-I', file('src/jni/jdkHeaders/linux').toString()
                    // overwrite standard headers with custom android headers
                    cppCompiler.args '-iquote', file('src/giac/headers/android').toString()
                    cppCompiler.args '-fno-strict-aliasing' // maybe not needed
                    cppCompiler.args '-DPIC' // maybe not needed
                    cppCompiler.args '-fPIC' // android 6.0 doesn't load libraries which have text relocations

                    linker.args '-s' // stripping
                }

                if (targetPlatform.operatingSystem.name == 'osx') {
                    cppCompiler.define 'HAVE_UNISTD_H'
                    cppCompiler.define 'APPLE_SMART'
                    cppCompiler.define 'NO_SCANDIR'
                    cppCompiler.define 'HAVE_SYS_TIMES_H'
                    cppCompiler.define 'HAVE_SYS_TIME_H'
                    cppCompiler.define 'gettext', ''
                    cppCompiler.args '-mmacosx-version-min=10.6'
                    cppCompiler.args '-isysroot', '/Applications/Xcode.app/Contents/Developer/Platforms/MacOSX.platform/Developer/SDKs/MacOSX10.12.sdk'
                    cppCompiler.args '-I/System/Library/Frameworks/CoreFoundation.framework/Headers'

                    cppCompiler.args '-I', file('src/jni/jdkHeaders/darwin').toString()

                    linker.args '-Wl,-search_paths_first'
                    linker.args '-L', file('src/jni/prebuilt/osx/x86-64').toString()

                    linker.args '-stdlib=libstdc++', '-lstdc++'
                    linker.args '-lgmp', '-lmpfr', '-lpthread', '-dynamiclib'
                    linker.args '-framework', 'Accelerate'
                    linker.args '-framework', 'CoreFoundation'
                    linker.args '-o', 'build/binaries/javagiacSharedLibrary/osx_amd64/libjavagiac.jnilib' // TODO: find a more elegant way
                }
            }
        }
    }
}

def lipoOutput(name, output) {
    def path = "build/libs/${name}/static"
    mkdir("${output}")
    exec_("lipo", 
        "${path}/ios_armv7/lib${name}.a", 
        "${path}/ios_arm64/lib${name}.a", 
        "${path}/ios_i386/lib${name}.a",
        "${path}/ios_x86_64/lib${name}.a",
        '-create', '-output', "${output}/lib${name}.a")
}

def lipoPrebuilt(name, output) {
    def path = "src/jni/prebuilt/ios"
    mkdir("${output}")
    exec_("lipo", 
        "${path}/armv7/lib${name}.a", 
        "${path}/arm64/lib${name}.a", 
        "${path}/i386/lib${name}.a",
        "${path}/x86_64/lib${name}.a",
        '-create', '-output', "${output}/lib${name}.a")
}

task simpleInterfaceIosStaticLibrary(dependsOn: [
    'simpleInterfaceIos_armv7StaticLibrary', 
    'simpleInterfaceIos_arm64StaticLibrary', 
    'simpleInterfaceIos_i386StaticLibrary',
    'simpleInterfaceIos_x86_64StaticLibrary'
])
task giacIosStaticLibrary(dependsOn: [
    'giacIos_armv7StaticLibrary', 
    'giacIos_arm64StaticLibrary', 
    'giacIos_i386StaticLibrary',
    'giacIos_x86_64StaticLibrary'
])

task createFatIosStaticLibrary(dependsOn: ['simpleInterfaceIosStaticLibrary', 'giacIosStaticLibrary']) {
    doLast {
        def output = "build/libs/lipo"
        lipoOutput("simpleInterface", output)
        lipoOutput("giac", output)
        lipoPrebuilt("mpfr", output)
        lipoPrebuilt("gmp", output)
    }
}

task zipIosLibrary(dependsOn: 'createFatIosStaticLibrary', type: Zip) {
    baseName 'Giac'
    from("src/simpleInterface/headers") {
        include "*.hpp"
        into("headers")
    }
    from("${buildDir}/libs/lipo") {
        include "libsimpleInterface.a", "libgiac.a", "libgmp.a", "libmpfr.a"
        into("libs")
    }
    from("src/simpleInterface/pod/Giac.podspec")
    destinationDir = file("${buildDir}/archives/")
}

task installMinigiacExecutable (dependsOn: 'installMinigiacLinux64Executable')

task testMinigiacExecutable (dependsOn: 'installMinigiacExecutable', type: Exec) {
    description 'Tests the minigiac executable.'
    workingDir 'src/test'
    commandLine './regression', '-r', '-s'
}

task androidCopyEabiLibjavagiacSo (dependsOn: 'javagiacAndroideabiSharedLibrary', type: Copy) {
    description 'Copies libjavagiac.so files to the src/android folder.'
    from 'build/libs/javagiac/shared/androideabi'
    into 'giac-android/src/main/jniLibs/armeabi-v7a'
    include ('libjavagiac.so')
}

task androidCopyX86LibjavagiacSo (dependsOn: 'javagiacAndroidx86SharedLibrary', type: Copy) {
    description 'Copies libjavagiac.so files to the src/android folder.'
    from 'build/libs/javagiac/shared/androidx86'
    into 'giac-android/src/main/jniLibs/x86'
    include ('libjavagiac.so')
}

task run (dependsOn: 'installMinigiacExecutable', type: Exec) {
    description "Runs Giac's minigiac terminal"
    commandLine "build/install/minigiac/linux64/minigiac" // FIXME, this is hardcoded
    standardInput = System.in
}

tasks.addRule("Pattern: emccCompile_<FILE>Cc: Compile <FILE>.cc into <FILE>.o.") { String taskName ->
    if ((taskName.startsWith('emccCompile_')) && (taskName.endsWith('Cc'))) {
        def basename = (taskName - 'Cc').substring('emccCompile_'.length())
        task(taskName) {
            def input = "src/giac/cpp/${basename}.cc"
            inputs.file input
            def output = "build/objs/giac.js/${basename}.o"
            outputs.file output
            doLast {
                file("build/objs/giac.js").mkdirs()
                exec {
                    // Be very careful when changing this: config.h also contains some entries! 
                    def specialOptions = ''
                    def commandline = emccCommand
                    commandline += ' -DIN_GIAC -DGIAC_GENERIC_CONSTANTS -DNO_STDEXCEPT -DHAVE_CONFIG_H' // from old Makefile
                    if (emscriptenVer == '201304') {
                        commandline += " -s PRECISE_I32_MUL=1" // from old Makefile (PREC)
                    }
                    if (emscriptenVer == '201505') {
                        commandline += " -Dgammaf=tgammaf -s ALLOW_MEMORY_GROWTH=1 -fno-exceptions"
                    }

                    commandline += ' -DGIAC_GGB' // from old Makefile
                    commandline += ' -DTIMEOUT -DEMCC' // from old config.h
                    commandline += " -Isrc/giac/headers -c $input -o $output"

                    commandLine commandline.split()
                }
            }
        }
    }
}

task emccClean(type: Delete) {
    description 'Deletes .o files and linked giac*.js for cleaning up.'
    delete 'build/objs/giac.js', 'build/binaries/giacggb.js'
}

task emccCompile {
    description 'Creates .o files for giac.js.'
    def list = []
    FileTree files = fileTree(dir: 'src/giac/cpp')
    files.visit { f ->
        if (f.name.endsWith('.cc')) {
            def emccCompileTask = 'emccCompile_' + f.name - '.cc' + 'Cc'
            list << emccCompileTask
        }
    }
    dependsOn list
}

task emccGiacJs (dependsOn: 'emccCompile') {
    description 'Links giac.js.'
    def list = []
    def linkerArgs = []
    def inputInclude = []
    FileTree files = fileTree(dir: 'build/objs/giac.js')
    files.visit { f ->
        def emccCompileTask = 'emccCompile_' + f.name - '.o' + 'Cc'
        inputInclude << f.name
        inputInclude << ('src/giac/cpp/' + f.name - '.o' + '.cc')
        list << emccCompileTask
        linkerArgs << "build/objs/giac.js/${f.name}"
        }
    def output = "build/binaries/giacggb.js/giacggb.js"
    inputs.files inputInclude
    outputs.file output
    mustRunAfter emccCompile
    doLast {
        file("build/binaries/giacggb.js").mkdirs()
        exec {
            linkerArgs << LlvmMpfrA // mpfr must precede gmp, see http://www.mpfr.org/faq.html, Q5
            linkerArgs << LlvmGmpA
            linkerArgs << '--js-library' << 'src/giac.js/js/time.js'
            if (emscriptenVer == '201304') {
                linkerArgs << '-s' << 'PRECISE_I32_MUL=1'
            }
            if (emscriptenVer == '201505') {
                linkerArgs << '-s' << 'ALLOW_MEMORY_GROWTH=1'
                // linkerArgs << '-s' << 'PRECISE_I64_MATH=1'
            }
            linkerArgs << '-DGIAC_GGB'
            linkerArgs << '-o' << output
            linkerArgs << '-O2' << '-v' << '-s' << "EXPORTED_FUNCTIONS=['_caseval']"
            linkerArgs << '--closure' << '1'
            commandLine emccCommand
            args linkerArgs
        }
    }
}

task createGiacGgbJs(dependsOn: 'emccGiacJs', type: Copy) {
    description 'Creates the giacggb.js folder to store embeddable giac.js.'
    from 'src/giac.js'
    into 'build/binaries/giacggb.js'
    include ('ggb.html')
}

task createGiacJs(dependsOn: 'createGiacGgbJs', type: Copy) {
    description 'Creates JavaScript version of Giac which can be embedded into GeoGebraWeb.'
    from 'build/binaries/giacggb.js'
    into 'build/binaries/giacggb.js'
    include ('giacggb.js')
    rename 'giacggb.js', 'giac.js'
    filter { line -> line.replace('Module', '__ggb__giac') }
}

// Using emsdk

task downloadEmsdk {
    description 'Downloads emscripten SDK and downloads it.'
    outputs.dir emsdkDir
    doLast {
        emsdkDir.mkdirs()
        ant.get(src: "$emsdkUrlFolder/$emsdkTgzFile", dest: emsdkDir,
            skipexisting: true, usetimestamp: true)
        copy {
            from tarTree("$emsdkDir/$emsdkTgzFile")
            into "$emsdkDir"
        }
    }
}

task installEmsdk (dependsOn: 'downloadEmsdk') {
    description 'Installs/updates the emscripten SDK.'
    doLast {
        exec {
            commandLine './emsdk update'.split()
            workingDir emsdkRunDir
        }
        exec {
            commandLine './emsdk install latest'.split()
            workingDir emsdkRunDir
        }
        exec {
            commandLine './emsdk activate latest'.split()
            workingDir emsdkRunDir
        }
        // "nodejs" must be set in ~/.emscripten under Ubuntu 14.10
    }
}

task emConfigureGmp {
    description 'Configures GMP for the emscripten SDK.'
    doLast {
        exec {
            commandLine 'bash', '-c', "source $emsdkRunDir/emsdk_env.sh; $emscriptenDir/emconfigure $gmpSourceDir/configure --build=none --host=none --disable-assembly"
            workingDir gmpSourceDir
        }
    }
}

task emMakeGmp {
    description 'Makes GMP with the emscripten SDK.'
    doLast {
        exec {
            commandLine 'bash', '-c', "source $emsdkRunDir/emsdk_env.sh; $emscriptenDir/emmake make"
            workingDir gmpSourceDir
        }
    }
}

task emConfigureMpfr {
    description 'Configures MPFR for the emscripten SDK.'
    doLast {
        exec {
            commandLine 'bash', '-c', "source $emsdkRunDir/emsdk_env.sh; " +
                "$emscriptenDir/emconfigure $mpfrSourceDir/configure --build=none --host=none --disable-assembly " +
                "--with-gmp-lib=$gmpSourceDir/.libs --with-gmp-include=$gmpSourceDir"
            workingDir mpfrSourceDir
        }
    }
}

task emMakeMpfr {
    description 'Makes MPFR with the emscripten SDK.'
    doLast {
        exec {
            commandLine 'bash', '-c', "source $emsdkRunDir/emsdk_env.sh; $emscriptenDir/emmake make"
            workingDir mpfrSourceDir
        }
    }
}

/* How to use the new system:
 * Run tasks downloadEmsdk, installEmsdk, emConfigureGmp, emMakeGmp, emConfigureMpfr, emMakeMpfr, then run
 * $ EMSCRIPTEN_VER=201505 EXT_GMPMPFR=emgiac EMCC=$HOME/geogebra/giac/build/emsdk/emsdk_portable/emscripten/master/emcc ../gradlew createGiacJs
 */

/* How to use the old system under Java 8, 2 CPUs:
 * $ EMCC=.../emscripten-master/emcc LLVM=.../clang+llvm-3.2-x86_64-linux-ubuntu-12.04/bin \
   PATH=.../cross-compilers/x86/bin:/.../cross-compilers/arm/bin:$PATH JAVA_HEAP_SIZE=3072m ../gradlew createGiacJs
 */


task androidCopyCrystaxSo (dependsOn: 'giac-android:copyCrystaxSo')

task androidAar (dependsOn: ['androidCopyCrystaxSo', 'androidCopyEabiLibjavagiacSo', 'androidCopyX86LibjavagiacSo', 'giac-android:assemble']) {
    description 'Creates .aar package'
}
