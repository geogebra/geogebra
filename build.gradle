project.description = 'GeoGebra'

subprojects {
	apply plugin: 'eclipse'
	apply plugin: 'java'

	repositories {
		maven {
			url 'http://dev.geogebra.org/maven2'
		}
		mavenCentral()
    }

	eclipse.project {
		natures 'org.springsource.ide.eclipse.gradle.core.nature'
	}

	// add auto formatter to every project
	task eclipseJdtPrepare(type: Copy) {
		description 'Copies Eclipse jdt.core preference file.'
		from rootProject.file('eclipse/org.eclipse.jdt.core.prefs')
		into project.file('.settings/')
		outputs.upToDateWhen { false }
	}

	task eclipseJdtUi(type: Copy) {
		description 'Copies Eclipse jdt.ui preference file.'
		from rootProject.file('eclipse/org.eclipse.jdt.ui.prefs')
		into project.file('.settings/')
		outputs.upToDateWhen { false }
	}

	task cleanEclipseJdtUi(type: Delete) {
		description 'Cleans Eclipse jdt.ui preference file.'
		delete project.file('.settings/org.eclipse.jdt.ui.prefs')
	}

	tasks['eclipseJdt'].dependsOn(eclipseJdtPrepare)
	tasks['cleanEclipse'].dependsOn(cleanEclipseJdtUi)
	tasks['eclipse'].dependsOn(eclipseJdtPrepare, eclipseJdtUi, cleanEclipse)
}

task wrapper(type: Wrapper) {
	description = 'Creates the gradle wrapper'
	gradleVersion = '2.3'
}

// eclipse related tasks
project(':common') {
	
	cleanEclipse.dependsOn('cleanJavacc')
	eclipseClasspath.dependsOn('compileJavacc')

	eclipse.project {
 		natures 'sf.eclipse.javacc.javaccnature'
	}
}

project(':web') {
	// exclude from the eclipse classpath the java files in the resources dir
	eclipse.classpath.file { 
		whenMerged { classpath ->
			classpath.entries.find { entry -> entry.kind == 'src' && entry.path == 'src/main/resources' }.excludes = ['**/*.java']
		}
	}
}

ext.joglVersion = '2.2.0'

// This is automatically updated when there is a new version of Giac out:
ext.javaGiacVersion = '42133'
// Please don't change this line since the automatism looks for exact match.
