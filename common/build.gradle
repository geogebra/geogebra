plugins {
    id 'java-library'
    id 'pmd'
    id 'checkstyle'
    id 'com.github.spotbugs' version '5.0.9'
    id 'ca.coglinc.javacc' version '2.4.0'
}

project.setDescription('Common parts of GeoGebra for various platforms')

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    javacc 'net.java.dev.javacc:javacc:7.0.10'
    api project(':editor-base')
    api project(':renderer-base')
    api 'com.google.j2objc:annotations:2.8'
    api 'com.github.spotbugs:spotbugs-annotations:4.7.1'
    api 'com.google.code.findbugs:jsr305:3.0.2'
}

sourceSets {
    nonfree
    gpl
    main {
        java {
            srcDir compileJavacc.outputDirectory
            srcDir compileJavacc.inputDirectory
        }
        resources {
            if (project.hasProperty("usegpl")) {
                srcDirs += gpl.resources.srcDirs
            } else {
                srcDirs += nonfree.resources.srcDirs
            }
        }
    }
}
if(hasProperty("eclipse")){
	eclipse.classpath.sourceSets.removeAll { it.name == 'nonfree' || it.name == 'gpl'}
}

compileJavacc {
	arguments = [static: 'false', JAVA_TEMPLATE_TYPE: 'modern', LEGACY_EXCEPTION_HANDLING: 'false']
}

compileJava {
	options.encoding = 'UTF-8'
}

task cleanJavacc(type: Delete) {
	description 'Cleans the javacc generated files.'
	delete compileJavacc
}

import java.text.DateFormat
import java.text.SimpleDateFormat

task versionBump(){
	doLast {
		def version = "undef"
		def constants = file('../common/src/main/java/org/geogebra/common/GeoGebraConstants.java')
	    constants.eachLine { line ->
	        def pattern = ~/ VERSION_STRING = "(.*)"/
	        def matcher = (line =~ pattern)
	        if (matcher) {
	            version = line.substring(matcher.start(1), matcher.end(1))
	        }
	    }
	    def parts = version.split("\\.").collect({f->Integer.parseInt(f)});
	    parts[2]++;
	    parts[3] = 0;
	    version = parts[0]+"."+parts[1]+"."+parts[2]+"."+parts[3];
		String t = constants.getText();
		DateFormat df = new SimpleDateFormat("dd MMMM Y", Locale.US);
		t = t.replaceAll("BUILD_DATE.*","BUILD_DATE = \""+df.format(new Date())+"\";");
		t = t.replaceAll("final String VERSION_STRING.*","final String VERSION_STRING = \""+version+"\";");
		constants.text = t;
		// version.txt for Jenkins build number
		file("build/").mkdirs()
		file("build/version.txt").text = version
	}
}

test {
     ignoreFailures = true
}

spotbugs {
    ignoreFailures = true
    excludeFilter = file("../config/spotbugs.xml")
    jvmArgs = ['-Dfindbugs.sf.comment=true']
}

spotbugsMain.maxHeapSize = '1g'

pmd {
    ignoreFailures = true
    ruleSets = []
    ruleSetFiles = files("../config/pmd.xml")
}
