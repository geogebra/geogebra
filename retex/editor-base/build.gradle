plugins {
    id 'java-library'
    id 'maven-publish'
    id 'pmd'
    id 'checkstyle'
    id "com.github.spotbugs" version "4.7.3"
    id 'ca.coglinc.javacc' version '2.4.0'
}

group 'com.himamis.retex'
version '0.1'

publishing {
	publications {
		library(MavenPublication) {
			from components.java
		}
	}
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
	javacc 'net.java.dev.javacc:javacc:7.0.10'
	api project(':renderer-base')
    implementation 'com.google.j2objc:annotations:2.8'
    testImplementation "junit:junit:$junitVersion"
}

sourceSets.main.java.srcDirs += [compileJavacc.outputDirectory, compileJavacc.inputDirectory]

compileJavacc {
	arguments = [static: 'false', grammar_encoding: 'UTF-8', unicode_input : 'true',  JAVA_TEMPLATE_TYPE: 'modern',
                 LEGACY_EXCEPTION_HANDLING: 'false']
}

compileJava {
	options.encoding = 'UTF-8'
}

task cleanJavacc(type: Delete) {
	description 'Cleans the javacc generated files.'
	delete compileJavacc
}

pmd {
     ignoreFailures = true
     ruleSets = []
     ruleSetFiles = files("../../config/pmd.xml")
}

spotbugs {
    ignoreFailures = true
    excludeFilter = file("../../config/spotbugs.xml")
}
