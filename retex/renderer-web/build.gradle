plugins {
	id 'java-library'
	id 'eclipse'
	id 'org.docstr.gwt' version '1.1.26'
}

ext.gwtCompilerVersion = '2.11.0'

gwt {
	maxHeapSize = '2000M'
	gwtVersion = gwtCompilerVersion
	// only compilable module
	modules 'com.himamis.retex.renderer.JLaTeXMathGWTExportedLibrary'
	devModules 'com.himamis.retex.renderer.JLaTeXMathGWTDev'
	compiler {
		if (project.hasProperty("gdetailed")) {
			style = org.docstr.gradle.plugins.gwt.Style.DETAILED
		} else {
			disableClassMetadata = true
		}
	}
	jsInteropExports {
		shouldGenerate = true;
	}
}

evaluationDependsOn ':renderer-base'
evaluationDependsOn ':gwtutil'

dependencies {
	api project(':renderer-base'),
			project(':gwtutil'),
			files(project(':renderer-base').sourceSets.main.java.srcDirs),
			files(project(':gwtutil').sourceSets.main.java.srcDirs),
			files(project(':gwtutil').sourceSets.main.resources.srcDirs),
			files(file("build/generated/sources/annotationProcessor/java/main/")),
			"org.gwtproject:gwt-dev:$gwtCompilerVersion",
			"org.gwtproject:gwt-user:$gwtCompilerVersion",
			'org.treblereel.gwt.gwtproject.widgets:gwt-widgets:202403181',
			'org.treblereel.gwt.gwtproject.widgets:gwt-widgets:202403181:sources',
			'org.gwtproject.core:gwt-core:1.0.0-RC2'
	testImplementation "org.gwtproject:gwt-user:$gwtCompilerVersion",
			'org.treblereel.gwt.gwtproject.widgets:gwt-widgets:202403181',
			'org.treblereel.gwt.gwtproject.widgets:gwt-widgets:202403181:sources'

	annotationProcessor project(":gwtutil"),
			'org.treblereel.gwt.gwtproject.resources:gwt-resources-processor:202403181'
}

compileJava.options.sourcepath = files(processResources.destinationDir).builtBy(processResources)

task run(type: org.docstr.gradle.plugins.gwt.GwtDev, dependsOn: ['jar']) {
	war = file('war')
	cacheDir = file('build/gwt/devModeCache')
	maxHeapSize = '4096m'
	description 'Starts a codeserver, and a simple webserver for development'
}

task jarAll(type: Jar) {
	archiveClassifier = 'all'
	from project.sourceSets.main.java
	from project.sourceSets.main.output
	from project(':renderer-base').sourceSets.main.java
	from project(':renderer-base').sourceSets.main.output
}

task jarSources(type: Jar, dependsOn: classes) {
	archiveClassifier = 'sources'
    from sourceSets.main.allSource 
}

// exclude from the eclipse classpath the java files in the resources dir
eclipse.classpath.file { 
	whenMerged { classpath ->
		classpath.entries.find { entry -> entry.kind == 'src' && entry.path == 'src/main/resources' }.excludes = ['**/*.java']
	}
}

task cleanDist(type: Delete) {
	delete 'dist'
}

task dist(type: Copy, dependsOn: cleanDist) {
	from compileGwt
	into 'dist'
	doLast {
		// rename library
		File jsFile = file('dist/jlatexmath/jlatexmath.nocache.js')
		File newFile = file('dist/jlatexmath/jlatexmath.js')
		jsFile.renameTo(newFile)

		// remove unneeded artifacts
		File clearCacheGif = file('dist/jlatexmath/clear.cache.gif')
		clearCacheGif.delete();
		File webInfFolder = file('dist/WEB-INF')
		deleteDir(webInfFolder)
	}
}

task zipDist(type: Zip, dependsOn: dist) {
	from 'dist/jlatexmath'
	destinationDirectory = file('dist/')
}

def deleteDir(dir) {
	if (dir.isDirectory()) {
        String[] children = dir.list();
        for (int i = 0; i < children.length; i++) {
            boolean success = deleteDir(new File(dir, children[i]));
            if (!success) {
                return false;
            }
        }
    }
    return dir.delete();
}

// https://issues.gradle.org/browse/GRADLE-2778
// http://discuss.gradle.org/t/javadoc-generation-failed-with-vaadin-dependency/2502/12
javadoc {
    options.addStringOption("sourcepath", "")
}
