plugins {
    id 'java'
    id 'org.wisepersist.gwt' version '1.1.16'
    id 'pmd'
    id 'checkstyle'
    id 'com.github.spotbugs' version '4.7.9'
}

description 'Web platform code which does not use the GWT Widgets library'

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile project(':common'),
            project(':gwtutil'),
            project(':renderer-base'),
            project(':editor-base'),
            project(':renderer-web'),
            project(':editor-web'),
            project(':ggbjdk'),
            project(':web:dev'),
            files(project(':gwtutil').sourceSets.main.java.srcDirs),
            files(project(':common').sourceSets.main.allSource.srcDirs),
            files(project(':ggbjdk').sourceSets.main.java.srcDirs),
            files(project(':editor-web').sourceSets.main.allSource.srcDirs),
            files(project(':web:dev').sourceSets.main.allSource.srcDirs),
            "fr.ujf-grenoble:giac-gwt:$javaGiacVersion",
            "fr.ujf-grenoble:giac-gwt:$javaGiacVersion:sources",
            "com.google.gwt:gwt-dev:$gwtCompilerVersion"
}

evaluationDependsOn(':common')
evaluationDependsOn(':ggbjdk')
evaluationDependsOn(':editor-web')
evaluationDependsOn(':keyboard-web')

spotbugs {
    ignoreFailures = true
    excludeFilter = file("../config/findbugs.xml")
    jvmArgs = ['-Dfindbugs.sf.comment=true']
}

pmd {
    ignoreFailures = true
    ruleSets = []
    ruleSetFiles = files("../config/pmd.xml")
}

gwt {
    gwtVersion = gwtCompilerVersion

    modules = ['org.geogebra.web.WebSimple']
    devModules = modules

    maxHeapSize = '2000M'

    jsInteropExports {
        shouldGenerate = true;
    }

    compiler {
        // Customize the GWT compiler here
        strict = true
        disableCastChecking = true

        if (project.hasProperty("greport")) {
            compileReport = true
        }
        if (project.hasProperty("gdraft")) {
            draftCompile = true
        }
        if (project.hasProperty("gworkers")) {
            localWorkers = project.getProperty("gworkers")
        }
        if (project.hasProperty("gdetailed")){
            style = org.wisepersist.gradle.plugins.gwt.Style.DETAILED
        } else {
            disableClassMetadata = true
        }
        if (project.hasProperty("gsoyc")) {
            soycDetailed = true
        }
    }
}
