








LocalSymbols(options)
[
    options  := {
	{"default", "data"},
	{"data", "Plot2DData"},
	{"java", "Plot2DJava"},
    {"geogebra", "Plot2DGeoGebra"},
    {"jfreechart", "Plot2DJFreeChart"},
};
    

Plot2DOutputs() := options;

];




Plot2DData(values_IsList, _optionsHash) <-- values;


Plot2DJava(values_IsList, _optionsHash) <--
[
  Local(result,count);
  count := 0;
  result:="$plot2d:";

  result := result:" pensize 2.0 ";
  ForEach(function,values)
  [
    result := result:ColorForGraphNr(count);
    count++;
    result:=result:" lines2d ":ToString(Length(function));

    function:=Select(function, Lambda({item},item[2] != Undefined));

    ForEach(item,function)
    [
      result := result:" ":ToString(item[1]):" ":ToString(item[2]):" ";
    ];
  ];
  WriteString(result:"$");
  True;
];

10 # ColorForGraphNr(0) <-- " pencolor 64 64 128 ";
10 # ColorForGraphNr(1) <-- " pencolor 128 64 64 ";
10 # ColorForGraphNr(2) <-- " pencolor 64 128 64 ";
20 # ColorForGraphNr(_count) <-- ColorForGraphNr(Modulo(count,3));





Plot2DGeogebra(values_IsList, _optionsHash) <--
[
  Local(result,count);
  count := 0;
  result:="";


  ForEach(function,values)
  [

    function:=Select(function, Lambda({item},item[2] != Undefined));

    ForEach(item,function)
    [
      result := result:"(":ToString(item[1]):",":ToString(item[2]):")":Nl();
    ];
  ];
  WriteString(result);
  True;
];






Plot2DJFreeChart(values_IsList, _optionsHash) <--
[
    Local(rangeList, domainList, function, allProcessedFunctionData, lineChartCallListForm);
    
    
    
    
    ForEach(name, {"xrange", "xname", "yname", "output", "precision", "points", "depth"})
    [
        AssocDelete(optionsHash, name);
    ];
    
    
    
    
    allProcessedFunctionData := {};
    
    ForEach(function,values)
    [
        rangeList := {};
        
        domainList := {};
        
        function := Select(function, Lambda({item},item[2] != Undefined));
        
        ForEach(item,function)
        [
            rangeList := Append(rangeList, item[1]);
          
            domainList := Append(domainList, item[2]);
        ];
        
        allProcessedFunctionData := Append(allProcessedFunctionData, rangeList);
        allProcessedFunctionData := Append(allProcessedFunctionData, domainList);
    
    ];


    
    
    lineChartCallListForm := {LineChart, allProcessedFunctionData };
    
    
    
    
    ForEach(key, AssocIndices(optionsHash))
    [
        lineChartCallListForm := Append(lineChartCallListForm, Apply("->", {key, optionsHash[key]}));
    ];
    
    
    
    
    Eval(ListToFunction(lineChartCallListForm));
    

];















LocalSymbols(var, func, range, option, options'list, delta, options'hash, c, fc, all'values, dummy)
[


Function() Plot2D(func);
Function() Plot2D(func, range);
Function() Plot2D(func, range, options, ...);


1 # Plot2D(_func) <-- ("Plot2D" @ {func, -5:5});
2 # Plot2D(_func, _range) <-- ("Plot2D" @ {func, range, {}});
3 # Plot2D(_func, _range, option_IsFunction) _ (Type(option) = "->" )  <-- ("Plot2D" @ {func, range, {option}});


5 # Plot2D(_func, _range, options'list_IsList)_(Not IsList(func)) <-- ("Plot2D" @ {{func}, range, options'list});



4 # Plot2D(func'list_IsList, _range, options'list_IsList) <--
[
	Local(var, func, delta, options'hash, c, fc, all'values, dummy);
	all'values := {};
	options'hash := "OptionsListToHash" @ {options'list};
    
    
	
	options'hash["xname"] := "";
	
	options'hash["yname"] := {};
	
	If (
		Type(range) = "->",	
		[
			
			options'hash["xname"] := ToString(range[1]);
			range := range[2];
		]
	);
	If(
		Type(range) = ":",	
		range := N(Eval({range[1], range[2]}))
	);
	
	If(
		options'hash["points"] = Empty,
		options'hash["points"] := 23
	);
	If(
		options'hash["depth"] = Empty,
		options'hash["depth"] := 5
	);
	If(
		options'hash["precision"] = Empty,
		options'hash["precision"] := 0.0001
	);
	If(
		options'hash["output"] = Empty Or IsString(options'hash["output"]) And Plot2DOutputs()[options'hash["output"]] = Empty,
		options'hash["output"] := Plot2DOutputs()["default"]
	);
	
	If(
		options'hash["output"] = "datafile" And options'hash["filename"] = Empty,
		options'hash["filename"] := "output.data"
	);
    
	
	options'hash["points"] := N(Eval(Quotient(options'hash["points"]+3, 4)));
    
	
	options'hash["precision"] := N(Eval(options'hash["precision"]));
    
	
	options'hash["xrange"] := {range[1], range[2]};
    
	
	delta := N(Eval( (range[2] - range[1]) / (options'hash["points"]) ));
    
	
	Check(IsNumber(range[1]) And IsNumber(range[2]) And IsNumber(options'hash["points"]) And IsNumber(options'hash["precision"]),
                "Argument",
		"Plot2D: Error: plotting range '"
		:(PipeToString()Write(range))
		:"' and/or the number of points '"
		:(PipeToString()Write(options'hash["points"]))
		:"' and/or precision '"
		:(PipeToString()Write(options'hash["precision"]))
		:"' is not numeric"
	);
	
	ForEach(func, func'list)
	[
		
		var := VarList(func);        
                Check(Length(var)<=1, "Argument", "Plot2D: Error: expression is not a function of one variable: "
			:(PipeToString()Write(func))
		);
		
		If(Length(var)=0, var:={dummy});
		
		If(
			options'hash["xname"] = "",
			options'hash["xname"] := ToString(VarList(var)[1])
		);
		
		DestructiveAppend(options'hash["yname"], PipeToString()Write(func));
		
		c := range[1];
		fc := N(Eval(Apply({var, func}, {c})));
		Check(IsNumber(fc) Or fc=Infinity Or fc= -Infinity Or fc=Undefined,
                        "Argument", 
			"Plot2D: Error: cannot evaluate function '"
			:(PipeToString()Write(func))
			:"' at point '"
			:(PipeToString()Write(c))
			:"' to a number, instead got '"
			:(PipeToString()Write(fc))
			:"'"
		);
		
		DestructiveAppend(all'values,  Plot2D'get'data(func, var, c, fc, delta, options'hash) );
        
		If(InVerboseMode(), Echo({"Plot2D: using ", Length(all'values[Length(all'values)]), " points for function ", func}), True);
	];
    
	
	Plot2DOutputs()[options'hash["output"]] @ {all'values, options'hash};
];



HoldArgumentNumber("Plot2D", 2, 2);
HoldArgumentNumber("Plot2D", 3, 2);
HoldArgumentNumber("Plot2D", 3, 3);







Plot2D'get'data(_func, _var, _x'init, _y'init, _delta'x, _options'hash) <--
[
	Local(i, a, fa, b, fb, c, fc, result);
	
	result := { {c,fc} := {x'init, y'init} };
	For(i:=0, i<options'hash["points"], i++)
	[
		{a,fa} := {c, fc};	
		
		{b, c} := N(Eval({x'init + (i+1/2)*delta'x, x'init + (i+1)*delta'x}));	
		{fb, fc} := N(Eval(MapSingle({var, func}, {b, c})));
		result := Concat(result,
			Rest(Plot2D'adaptive(func, var, {a,b,c}, {fa, fb, fc}, options'hash["depth"],
				
				options'hash["precision"]*options'hash["points"] )));
	];
    
	result;
];







Plot2D'adaptive(_func, _var, {_a,_b,_c}, {_fa, _fb, _fc}, _depth, _epsilon) <--
[
	Local(a1, b1, fa1, fb1);

	a1 := N(Eval((a+b)/2));
	b1 := N(Eval((b+c)/2));
	{fa1, fb1} := N(Eval(MapSingle({var, func}, {a1, b1})));
	If(
		depth<=0 Or
		(
		  
		  sign'change(fa, fa1, fb) + sign'change(fa1, fb, fb1) + sign'change(fb, fb1, fc) <= 2
		  And
		  
		  N(Eval(Abs( (fa-5*fa1+9*fb-7*fb1+2*fc)/24 ) ))	
		  	<= N(Eval( epsilon*(	
		    
			(5*fb+8*fb1-fc)/12	
			- Minimum({fa,fa1,fb,fb1,fc}) ) ) )
		),
		
		{{a,fa}, {a1,fa1}, {b,fb}, {b1,fb1}, {c,fc}},
		
		Concat(
			
			Plot2D'adaptive(func, var, {a, a1, b}, {fa, fa1, fb}, depth-1, epsilon*2),	
			Rest(Plot2D'adaptive(func, var, {b, b1, c}, {fb, fb1, fc}, depth-1, epsilon*2))
		)
	);
];

];	


