import de.aaschmid.gradle.plugins.cpd.Cpd
import io.miret.etienne.gradle.sass.CompileSass
import org.apache.tools.ant.taskdefs.condition.Os
import org.docstr.gradle.plugins.gwt.AbstractGwtActionTask
import org.docstr.gradle.plugins.gwt.GwtDev

plugins {
    alias libs.plugins.geogebra.java
    alias libs.plugins.geogebra.gwt
    alias libs.plugins.geogebra.pmd
    alias libs.plugins.geogebra.checkstyle
    alias libs.plugins.geogebra.spotbugs
    alias libs.plugins.geogebra.webjars
    alias libs.plugins.geogebra.sourcesets
    alias libs.plugins.geogebra.javadoc.workaround
    alias libs.plugins.cpd
    alias libs.plugins.sass
}

description 'Parts of GeoGebra related to web platforms'

compileJava.options.sourcepath = files(processResources.destinationDir).builtBy(processResources)

dependencies {
    implementation project(':carota-web')
    implementation project(':web-dev')
    implementation project(':web-common')
    implementation project(':editor-web')
    implementation project(':keyboard-web')
    implementation libs.pdfJs
    implementation libs.multiplayer
    implementation files(file("build/generated/sources/annotationProcessor/java/main/"))

    testImplementation libs.junit
    testImplementation libs.gwt.user
    testImplementation libs.gwt.mockito

    annotationProcessor project(":gwt-generator")
    annotationProcessor libs.gwt.resources.processor
}

def devModule = project.findProperty("gmodule") ?: 'org.geogebra.web.SuperWeb'

ext.appSpecs = [
        [id           : 'classic', title: 'Classic', transKey: 'Classic', exe: 'GeoGebra', pkg: 'GeoGebra_6.0',
         releases     : 'Releases', icon: 'ggb.ico', measurementId: 'G-8JGZQG6FHG',
         firebaseAppId: '1:895270214636:web:3c05deeefcd1a96584fec9'],
        [id           : 'graphing', title: 'Graphing Calculator', transKey: 'GraphingCalculator',
         exe          : 'GeoGebraGraphing', pkg: 'GeoGebra_Graphing', releases: 'Releases-graphing',
         icon         : 'Graphing.ico', appStoreId: '1146717204', measurementId: 'G-55K7P9GRQK',
         firebaseAppId: '1:895270214636:web:db7055c9a67f1f1e84fec9'],
        [id           : '3d', title: '3D Graphing Calculator', transKey: 'Graphing3D', appStoreId: '1445871976',
         firebaseAppId: '1:895270214636:web:574d60209dda5f5384fec9',
         measurementId: 'G-PMP3RB4M8S'],
        [id           : 'cas', title: 'CAS Calculator', transKey: 'CASCalculator',
         exe          : 'GeoGebraCAS', pkg: 'GeoGebra_CAS', releases: 'Releases-cas',
         icon         : 'CAS.ico', appStoreId: '1436278267', measurementId: 'G-N3Z7S82FZ6',
         firebaseAppId: '1:895270214636:web:87b00b628121939d84fec9'],
        [id           : 'scientific', title: 'Scientific Calculator', transKey: 'ScientificCalculator',
         partials     : ["undo-redo"], appStoreId: '1412748754', measurementId: 'G-YJ1QVF1SFK',
         firebaseAppId: '1:895270214636:web:540b533d4b47a7bb84fec9'],
        [id           : 'geometry', title: 'Geometry', transKey: 'Geometry',
         exe          : 'GeoGebraGeometry', pkg: 'GeoGebra_Geometry', releases: 'Releases-geometry',
         icon         : 'Geometry.ico', appStoreId: '1232591335', measurementId: 'G-82GL4RTR4H',
         firebaseAppId: '1:895270214636:web:6bf3d469e0d2b32584fec9'],
        [id           : 'suite', title: 'Calculator Suite', transKey: 'CalculatorSuite',
         firebaseAppId: '1:895270214636:web:7daf432883a8464084fec9', icon: 'ggb.ico',
         measurementId: 'G-FXVLXMD21Y', appStoreId: '1504416652',
         pkg          : 'GeoGebra_Calculator', exe: 'GeoGebraCalculator', releases: 'Releases-suite'],
        [id           : 'notes', title: 'Notes', transKey: 'Notes', exe: 'GeoGebraNotes', pkg: 'GeoGebra_Notes',
         releases     : 'Releases-notes', icon: 'notes.ico', measurementId: 'G-MWHBRQSXTL',
         firebaseAppId: '1:895270214636:web:aece5066924cacb084fec9'],
        [id  : 'notes-mebis', title: 'Tafel', transKey: 'Tafel', exe: 'MebisTafel', pkg: 'Mebis_Tafel',
         icon: 'notes.ico', vendor: "Mebis"]]

compileSass {
    // Directory where to output generated CSS:
    outputDir = file("${projectDir}/war/css")

    // Source directory containing sass to compile:
    sourceDir = file("${projectDir}/src/main/resources/scss")

    // Set the output style:
    // Possible values are “expanded” and “compressed”, default is “expanded”.
    style = compressed

    // Source map style:
    //  - file: output source map in a separate file (default)
    //  - embed: embed source map in CSS
    //  - none: do not emit source map.
    sourceMap = none
}

tasks.register('watchSass', CompileSass) {
    outputDir = file("${projectDir}/war/css")
    sourceDir = file("${projectDir}/src/main/resources/scss")
    style = compressed
    sourceMap = embed

    // Watch sass files in sourceDir for changes
    // (Default is to not to watch, compile once and terminate)
    watch()
}

def warDirRel = 'war'

gwt {
    if (project.hasProperty("gmodule")) {
        modules = project.getProperty("gmodule").split(",")
    } else {
        modules = ['org.geogebra.web.SuperWeb', 'org.geogebra.web.WebSimple', 'org.geogebra.web.Web',
                   'org.geogebra.web.Tablet3D']
    }

    devModules devModule
    sourceLevel = "11"

    dev {
        bindAddress = project.findProperty("gbind") ?: 'localhost'
        superDevMode = true
    }
}

tasks.withType(AbstractGwtActionTask).configureEach {
    jvmArgs '-Xss512M'
    useToolchain = true
}


def outputs = ['web', 'web3d', 'webSimple', 'tablet3d', 'tabletWin']

tasks.register('cleanWar', Delete) {
    doLast {
        outputs.each { folder ->
            delete warDirRel + "/$folder"
        }
    }
}

apply from: "templates.gradle"

def createHtml = { inlineCss ->
    delete {
        delete fileTree(dir: 'war', include: "*.html")
    }

    copy {
        from file("../common/src/main/resources/giac")
        into warDirRel
    }
    copy {
        from file("src/main/resources/org/geogebra/web/resources/war")
        into warDirRel
    }

    appSpecs.each { app ->
        def replaceHtml = app.id == "classic" ? classicHtml : appHtml
        file("war/${app.id}-offline.html").text = english(replaceHtml(app, false, true, inlineCss), app.title, "")
        file("war/${app.id}.html").text = english(replaceHtml(app, true, false, inlineCss), app.title, "en")
        if (app.vendor == "Mebis") {
            file("war/${app.id}-template.html").text = replaceHtml(app, false).replace("<!--CODEBASE-->", "/")
        }
    }
    file("war/calculator.html").text = file("war/suite.html").text
    file("war/notes-murokdev.html").text = file("war/notes.html").text.replace('<!--LANGUAGE LINKS-->', '<script src="http://localhost:8080/murok.js"></script>');
}

tasks.register('copyHtml') {
    dependsOn 'compileSass'
    description 'Generate app.html, app-release.html, graphing.html and others'
    group 'application'

    doLast {
        createHtml(true)
    }
}

tasks.register('copyDevHtml') {
    dependsOn 'compileSass'
    description 'Generate app.html, app-release.html, graphing.html and others without inlined css'
    group 'application'

    doLast {
        createHtml(false)
    }
}

tasks.register('copyHandlebars') {
    dependsOn 'compileSass'
    doLast {
        def handlebarsDir = project.getProperty("handlebarsDir")
        appSpecs.each { app ->
            def replaceHtml = app.id == "classic" ? classicHtml : appHtml

            def templatePath = "${handlebarsDir}/${app.id}-template.handlebars"
            file(templatePath).text = handlebars(replaceHtml(app, false, false, true))
        }
    }
}

tasks.register('prepareS3Upload') {
    dependsOn('compileGwt', 'copyHtml')
    doLast {
        ["web3d", "webSimple", "editor"].each { module ->
            def dir = file("build/gwt/out/$module")
            if (dir.exists()) {
                fileTree(dir).each { f ->
                    def outFile = file("$f".replace(dir.absolutePath, "build/s3/$module/"))
                    file(outFile.parent).mkdirs()
                    ant.gzip(src: f, zipfile: outFile)
                }
            }
            def srcMaps = file("build/gwt/out/WEB-INF/deploy/$module/symbolMaps/")
            file("build/symbolMapsGz/$module").mkdirs()
            if (srcMaps.exists()) {
                fileTree(srcMaps).each { f ->
                    def outFile = file("$f".replace(srcMaps.absolutePath, "build/symbolMapsGz/$module/"))
                    ant.gzip(src: f, zipfile: outFile)
                }
            }
        }
    }
}

tasks.register('deployIntoWar', Copy) {
    dependsOn('cleanWar', 'copyHtml')
    description 'Copies/updates the GWT production compilation directory (web3d, ...) to the war directory.'
    from compileGwt
    into warDirRel

    into("web3d") {
        from "$warDirRel/WEB-INF/deploy/web3d/symbolMaps"
    }

}

tasks.register('mergeDeploy') {
    description 'merge deploy.js and web3d.nocache.js'
    mustRunAfter 'compileGwt'
    doLast {
        def firstFolder = { files -> files.listFiles() ? files.listFiles()[0].name : "" };
        def web3dPermutation = firstFolder(file("./build/gwt/out/web3d/deferredjs"))
        def webSimplePermutation = firstFolder(file("./build/gwt/out/webSimple/deferredjs"))
        def templateJs = "src/main/resources/org/geogebra/web/resources/js/deployggb-template.js"
        def deployText = file(templateJs).text.replace('%WEB3D_PERMUTATION%', web3dPermutation)
                .replace('%WEBSIMPLE_PERMUTATION%', webSimplePermutation)
        def parentRoot = project(":common").getBuildDir().getAbsolutePath() + "/../../"
        def currentVersion = getGeogebraVersion(parentRoot)
        def localPath = project.findProperty("deployggbRoot") ?: './'
        file(warDirRel + "/deployggb.js").text = deployText.replace('%MODULE_BASE%', localPath)
        file(warDirRel + "/deployggb-latest.js").text = deployText.replace('%MODULE_BASE%', 'https://www.geogebra.org/apps/latest/')
        file(warDirRel + "/deployggb-${currentVersion}.js").text = deployText.replace('%MODULE_BASE%', "https://www.geogebra.org/apps/${currentVersion}/")
    }
}

def gwtGlobalCache = file('build/gwt/devModeCache')

class ReadableDev extends GwtDev {
    protected void addArgs() {
        super.addArgs();
        args("-style", "PRETTY");
    }
}

tasks.register('run', ReadableDev) {
    dependsOn('jar', 'copyDevHtml')
    war = file(warDirRel)
    maxHeapSize = '4096m'
    logLevel = 'TRACE'
    cacheDir = gwtGlobalCache
    extraJvmArgs = ["-Djava.io.tmpdir=${gwtGlobalCache.absolutePath}"]
    description 'Starts a codeserver, and a simple webserver for development'
    Process sassCompilation
    doFirst {
        delete {
            delete gwtGlobalCache
        }
        gwtGlobalCache.mkdirs()
        ProcessBuilder pb = new ProcessBuilder(Os.isFamily(Os.FAMILY_WINDOWS)
                ? ['cmd', '/c', 'START', '/MIN', file('..\\gradlew.bat').absolutePath, 'watchSass']
                : [file('../gradlew').absolutePath, 'watchSass'])
        sassCompilation = pb.start()
    }
    doLast {
        sassCompilation.destroy()
    }
}

tasks.register('createDraftBundleZip', Zip) {
    dependsOn 'compileSass'
    description 'Creates the Math Apps Bundle file.'
    destinationDirectory = file(warDirRel)
    archiveBaseName = "geogebra-bundle"
    from compileGwt {
        include("web3d/**")
        into "GeoGebra/HTML5/5.0"
    }
    from("war/css") {
        include "**"
        into "css"
    }
}

tasks.register('minifyLibs') {
    doLast {
        def libDir = "src/main/resources/org/geogebra/web/"
        def libDirCommon = "../web-common/$libDir"
        def libs = ["$libDirCommon/resources/js/rewrite_pHYs", "$libDir/pub/js/canvas2pdf", "$libDir/pub/js/whammy"]
        libs.each({ lib ->
            def command = ['npx', 'terser', "${lib}.js", "-o", "${lib}.min.js",
                           '--compress', '--mangle', '--comments', '/license/']
            exec { commandLine Os.isFamily(Os.FAMILY_WINDOWS) ? ['cmd', '/c'] + command : command }
        })
    }
}

test {
    ignoreFailures = true
    maxHeapSize = "1024m"
}

cpd {
    ignoreFailures = true
    minimumTokenCount = 100
}

tasks.withType(Cpd).configureEach {
    exclude "**/*.html"
    exclude "**/*.jj"
    exclude "**/*.xml"
    exclude "**/*.svg"
}
