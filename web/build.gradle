
buildscript {
    dependencies {
        classpath 'org.wisepersist:gwt-gradle-plugin:1.0.9'
    }
}

plugins {
    id 'java'
    id 'pmd'
    id 'checkstyle'
    id 'com.github.spotbugs' version '1.6.9'
    id 'de.aaschmid.cpd' version '2.0'
}

description 'Parts of GeoGebra related to web platforms'

evaluationDependsOn(':common')
evaluationDependsOn(':ggbjdk')
evaluationDependsOn(':editor-web')
evaluationDependsOn(':web:dev')
evaluationDependsOn(':keyboard-web')

ext.gwtCompilerVersion = project(':renderer-web').gwtCompilerVersion

apply plugin: 'gwt-compiler'

sourceCompatibility = 1.7
targetCompatibility = 1.7

sourceSets {
    nonfree
    gpl
    main {
        resources {
            if (project.hasProperty("usegpl")) {
                srcDirs += gpl.resources.srcDirs
            } else {
                srcDirs += nonfree.resources.srcDirs
            }
        }
    }
}

if(hasProperty("eclipse")){
    eclipse.classpath.sourceSets.removeAll { it.name == 'nonfree' || it.name == 'gpl'}
}

dependencies {
    compile project(':web:dev'), 
        project(':common'),
        project(':renderer-base'),
        project(':editor-base'),
        project(':renderer-web'),
        project(':editor-web'),
        project(':keyboard-web'),
        project(':ggbjdk'),
        files(project(':web:dev').sourceSets.main.allSource.srcDirs),
        files(project(':common').sourceSets.main.allSource.srcDirs),
        files(project(':ggbjdk').sourceSets.main.java.srcDirs),
        files(project(':editor-web').sourceSets.main.allSource.srcDirs),
        files(project(':keyboard-web').sourceSets.main.allSource.srcDirs),
        'com.googlecode.gwtphonegap:gwtphonegap:3.5.0.1',
        'com.googlecode:gwtgl:0.9.1',
        'com.googlecode:gwtgl:0.9.1:sources',
        "fr.ujf-grenoble:giac-gwt:$javaGiacVersion",
        "fr.ujf-grenoble:giac-gwt:$javaGiacVersion:sources",
        "com.google.gwt:gwt-dev:$gwtCompilerVersion",
        'com.asual.lesscss:lesscss-engine:1.3.0'

    testCompile 'junit:junit:4.+', 
        'com.google.gwt.gwtmockito:gwtmockito:1.1.8',
        "com.google.gwt:gwt-user:$gwtCompilerVersion"
}

def devModule = project.findProperty("gmodule") ?: 'org.geogebra.web.SuperWeb'

ext.appSpecs = [
    [id:'classic', title:'Classic', transKey:'Classic', exe:'GeoGebra', pkg:'GeoGebra_6.0', releases: 'Releases', icon: 'ggb.ico'],
    [id:'graphing', title:'Graphing Calculator', transKey:'GraphingCalculator',
     exe:'GeoGebraGraphing', pkg:'GeoGebra_Graphing', releases: 'Releases-graphing', icon:
             'Graphing.ico', appStoreId:'1146717204'],
    [id:'3d', title:'3D Graphing Calculator', transKey:'Graphing3D', appStoreId:'1445871976'],
    [id:'cas', title:'CAS Calculator', transKey:'CASCalculator'],
    [id:'calculator', title:'Scientific Calculator', transKey:'ScientificCalculator',
     partials:["undo-redo"], appStoreId:'1412748754'],
    [id:'geometry', title:'Geometry', transKey:'Geometry', exe:'GeoGebraGeometry',
     pkg:'GeoGebra_Geometry',  releases: 'Releases-geometry', icon: 'Geometry.ico', appStoreId:'1232591335'],
    [id:'notes', title:'Notes', transKey: 'Notes', exe:'GeoGebraNotes',  pkg:'GeoGebra_Notes',  releases: 'Releases-notes', icon: 'notes.ico'],
    [id:'notes-mebis', title:'Tafel', transKey: 'Tafel', exe:'MebisTafel',  pkg:'Mebis_Tafel', icon: 'notes.ico', vendor: "Mebis"]]

gwt {
    gwtVersion = gwtCompilerVersion

    if (project.hasProperty("gmodule")) {
        modules = project.getProperty("gmodule").split(",")
    } else {
        modules = ['org.geogebra.web.Web3D', 'org.geogebra.web.WebSimple', 'org.geogebra.web.Web',
          'org.geogebra.web.Tablet3D', 'org.geogebra.web.TabletWin', 'org.geogebra.web.Solver']
    }
    
    devModules devModule
    
    maxHeapSize = '2000M'

    compiler {
        // Customize the GWT compiler here
        strict = true
        // enableClosureCompiler = true;        
        disableCastChecking = true
      
        if (project.hasProperty("greport")) {
           compileReport = true
        }
        
        //    logLevel = 'SPAM';
        if (project.hasProperty("gdraft")) {
            draftCompile = true
        }
        if (project.hasProperty("gworkers")) {
            localWorkers = project.getProperty("gworkers")
        }
        if (project.hasProperty("gdetailed")){
            style = org.wisepersist.gradle.plugins.gwt.Style.DETAILED
        } else {
            disableClassMetadata = true
        }
        if (project.hasProperty("gsoyc")) {
            soycDetailed = true
        }
        // style = de.richsource.gradle.plugins.gwt.Style.PRETTY;    
    }
}

tasks.withType(org.wisepersist.gradle.plugins.gwt.AbstractGwtActionTask) {
    jvmArgs '-Xss512M'
}

def warDirRel = 'war'
def warDirAbs = file(warDirRel).toURI()
def outputs = ['web', 'web3d', 'webSimple', 'solver', 'tablet3d','tabletWin']

task cleanWar(type: Delete) {
    doLast {
      outputs.each { folder ->
          delete warDirRel + "/$folder"
      }
    }
}

def template = { fileName ->
    String templateDir = "src/main/resources/org/geogebra/web/resources/html/"
    return file(templateDir + fileName).text
}

def headerPartials = { header, appPartials ->
    def partials = appPartials ?: ["share-button", "signin-button"]
    partials.each({
        header = header.replace("<!--${it.toUpperCase()}-->", template("partials/${it}.html"));
    })
    return header
}

task copyHtml {
    description 'Generate app.html, app-release.html, graphing.html and others'
    group 'application'

    doLast {
        delete {
            delete fileTree(dir:'war', include: "*.html");
        }

        copy {
            from file("../common/src/main/resources/giac")
            into warDirRel
        }
        copy {
            from file("src/main/resources/org/geogebra/web/resources/war")
            into warDirRel
        }

        String html = template("app-template.html")
        String splash = template("classic-splash.html")

        ["algebra","graphics","spreadsheet","cas","probability","3d","exam"].each{
            def iconText = file("../common/src/nonfree/resources/org/geogebra/common/icons/svg/web/menuIconsTransparent/menu_view_"+it+".svg").text
            splash = splash.replace("<!--ICON:"+it.toUpperCase()+"-->", 
                "data:image/svg+xml;base64," + iconText.bytes.encodeBase64().toString())
        }
        def replaceVendor ={htmlContent, vendor ->
            return htmlContent.replace("<!--VENDOR-->", vendor)
            .replace("<!--VENDOR SCRIPTS-->", template("partials/vendor-scripts-${vendor.toLowerCase()}.html"))
            .replace("<!--PRECONNECT-->", vendor == "Mebis" ? "" : template("partials/preconnect.html"))
            .replace("<!--CODEBASE-->", vendor == "Mebis" ? "/" : "https://cdn.geogebra.org/apps/latest/")
        }
        def classicHtml = {app, prerelease, offline=false ->
            
           def platformSpecific = offline ? "<!--PLATFORM SPECIFIC CODE--><script src=\"platform.js\"></script>" : "<!--LANGUAGE LINKS-->"
           return replaceVendor(html, "GeoGebra").replace("<!--SPLASH-->", splash)
            .replace("<!--SPLASH STYLE-->", template("splash-style.css"))
            .replace("<!--ICON-->", "//cdn.geogebra.org/apps/icons/geogebra.ico")
            .replace("<!--PLATFORM SPECIFIC CODE-->", "<script>prerelease=${prerelease};appOnline=${!offline};</script>" + platformSpecific)
        }
        def appHtml = { app, prerelease, offline=false ->
            def vendor = app.vendor ?: "GeoGebra"
            def appID = app.id.split("-")[0]
            def appTransKey = app.transKey
            def header = vendor == "Mebis" ? "" : template("app-header-beta.html")
            header = headerPartials(header, app.partials)
            def platformSpecific = offline ? "<script src=\"platform.js\"></script>" : "<!--LANGUAGE LINKS-->"
            def stylesheet = String.valueOf(prerelease).contains("canary") ? "<link rel=\"stylesheet\" href=\"debug.css\"/>\n" :""
            def startscreenStyle = "startscreen " + appID + (offline ? " offline" : "")
            def appStoreBanner = app.appStoreId ? "<meta name=\"apple-itunes-app\" " +
                    "content=\"app-id=${app.appStoreId}\"><link rel=\"manifest\" href=\"https://www.geogebra.org/apps/manifest/${appID}.json\">" : ""
            return replaceVendor(html, vendor)
            .replace("<!--SPLASH-->", header + "<div class=\"$startscreenStyle\">" + template(app.id+"-splash.html") + "</div>")
            .replace("<!--SPLASH STYLE-->", template("app-header.css").replace('$height', offline ? '0' : '64px') + template("app-style.css"))
            .replace("<!--PLATFORM SPECIFIC CODE-->", stylesheet + "<script>prerelease=${prerelease};appID=\"${appID}\";appOnline=${!offline};</script>" + platformSpecific)
            .replace("<!--ICON-->", "//cdn.geogebra.org/apps/icons/${appID}.ico")
            .replace("<!--APP ID-->", appID)
            .replace("<!--APP TRANSKEY-->", appTransKey)
            .replace("<!--APPSTORE BANNER-->", appStoreBanner)
            .replace("\r\n", "\n") // generate the same on Win and Unix
        }
        def english = {raw, appName ->
            return raw.replace("<!--HOME-->","Home").replace("<!--DOWNLOAD-->","Download")
            .replace("<!--APP NAME FULL-->", "GeoGebra ${appName}")
            .replace("<!--SIGN IN-->", "Sign in")
            .replace("<!--APP NAME-->", appName)
            .replace("<!--LANG-->", "en")
        }

        if(!file("war/debug.css").exists()){
            file("war/debug.css").text = "/** Put your temporary CSS here */"
        }
        appSpecs.each({app->
            def appID = app.id.split("-")[0]
            def replaceHtml = app.id == "classic" ? classicHtml : appHtml
            // TODO notes features should go to stable, then we simplify this
            file("war/${app.id}-template.html").text = replaceHtml(app, "notes".equals(appID))
            file("war/${app.id}-template-beta.html").text = replaceHtml(app, true)
            // TODO notes features should go to stable, then we simplify this
            file("war/${app.id}-offline.html").text = english(replaceHtml(app, "notes".equals(appID), true), app.title)
            file("war/${app.id}.html").text = english(replaceHtml(app, true), app.title)
            file("war/${app.id}-canary.html").text = english(replaceHtml(app, "\"canary\""), app.title)
        });
    }
}

task createSolverHtml {
    doLast {
        def header = headerPartials(template("app-header-beta.html"), null);
        String solver = template("solver-template.html")
            .replace("<!--HEADER-->", header)
            .replace("<!--SPLASH STYLE-->", template("app-header.css"))
        // for server rendering
        file("war/solver-template.html").text = solver.replace("<!--APP TYPE-->", "solver")
        file("war/practice-template.html").text = solver.replace("<!--APP TYPE-->", "practice")
        // for direct use autotest
        file("war/solver.html").text = solver
            .replace("<!--APP NAME-->", "Math Solver")
            .replace("<!--APP TYPE-->", "solver")
            .replace("<!--SIGN IN-->", "Sign In")
            .replace("<!--LANGUAGE LINKS-->", "<script>codebase=\"./\"</script>")
        file("war/exercise.html").text = solver
            .replace("<!--APP NAME-->", "Exercise app")
            .replace("<!--SIGN IN-->", "Sign In")
            .replace("<!--APP TYPE-->", "practice")
            .replace("<!--LANGUAGE LINKS-->", "<script>codebase=\"./\"</script>")
    }
}

task deployIntoWar(type: Copy, dependsOn: ['cleanWar','copyHtml', 'createSolverHtml']) {
    description 'Copies/updates the GWT production compilation directory (web3d, ...) to the war directory.'
    from compileGwt
    into warDirRel
}

task mergeDeploy(){
   description 'merge deploy.js and web3d.nocache.js'
   mustRunAfter 'compileGwt'
   doLast {
      def web3dPermutation = file("./build/gwt/out/web3d/deferredjs").listFiles()[0].name
      def webSimplePermutation = file("./build/gwt/out/webSimple/deferredjs").listFiles()[0].name
      def templateJs = "src/main/resources/org/geogebra/web/resources/js/deployggb-template.js"
      def deployText = file(templateJs).text.replace('%WEB3D_PERMUTATION%', web3dPermutation)
                          .replace('%WEBSIMPLE_PERMUTATION%', webSimplePermutation)
      def parentRoot = project(":common").getBuildDir().getAbsolutePath() + "/../../"
      def currentVersion = getGeogebraVersion(parentRoot)
      deployText += file("./build/gwt/out/web3d/js/webfont.js").text
      file(warDirRel + "/deployggb.js").text = deployText.replace('%MODULE_BASE%','')
      file(warDirRel + "/deployggb-latest.js").text = deployText.replace('%MODULE_BASE%','https://cdn.geogebra.org/apps/latest/')
      file(warDirRel + "/deployggb-${currentVersion}.js").text = deployText.replace('%MODULE_BASE%',"https://cdn.geogebra.org/apps/${currentVersion}/")
   }
}

task symlinkIntoWar(dependsOn: ['copyHtml']) {
    description 'Creates a symbolic link to the GWT production compilation directory (web3d, ...) in the war directory. [UNUSED]'
    doLast {
        outputs.each { folder ->
            ant.symlink(resource: "../build/gwt/out/$folder", link: file("war/$folder"), overwrite: true)
        }
    }
}

task draftSymlinkIntoWar(dependsOn: ['copyHtml']) {
    description 'Creates a symbolic link to the GWT draft compilation directory (web3d) in the war directory.'
    doLast {
        outputs.each { folder ->
            ant.symlink(resource: "../build/gwt/draftOut/$folder", link: file("war/$folder"), overwrite: true)
        }
    }
}

task draftDeployIntoWar(type: Copy, dependsOn: ['cleanWar','copyHtml']) {
    description 'Copies/updates the GWT draft compilation directory (web3d) to the war directory.'
    from draftCompileGwt
    into warDirRel
}

def appHtmlRel = 'app.html'
def appHtmlAbs = file("${warDirAbs}/${appHtmlRel}").toURI()

task run(dependsOn: 'deployIntoWar') {
    group 'application'
    description 'Runs GeoGebraWeb in the default web browser'
    doLast {
        java.awt.Desktop.desktop.browse appHtmlAbs
    }
}

task draftRun(dependsOn: 'draftDeployIntoWar') {
    group 'application'
    description 'Runs GeoGebraWeb draft in the default web browser'
    doLast {
        java.awt.Desktop.desktop.browse appHtmlAbs
    }
}

def gwtGlobalCache = file('build/gwt/devModeCache');
// As now this task doesn't finish until the java process exits.
// This causes some problems as the IDE cannot run any other tasks.
// https://issues.gradle.org/browse/GRADLE-1254
task runSuperDev(type: JavaExec, dependsOn: ['jar','copyHtml', 'createSolverHtml']) {
    description 'Starts a codeserver, and a simple webserver for development'
    doFirst {
        delete {
            delete gwtGlobalCache
        }
        gwtGlobalCache.mkdirs()
    }
    main 'com.google.gwt.dev.DevMode'
    maxHeapSize '4096m'
    classpath ((configurations.compile as Set<String>) 
        + (sourceSets.main.allSource.srcDirs as Set<String>) 
        + (jar.archivePath as String))
    workingDir file(warDirRel)
    args '-logLevel', 'TRACE',
        '-codeServerPort', '9997',
        '-port', '8888',
        '-style', 'PRETTY',
        '-bindAddress', project.findProperty("gbind") ?: 'localhost',
        '-superDevMode',
        '-war', file(warDirRel),
        devModule
    systemProperty 'gwt.persistentunitcachedir', '../build/gwt/devModeCache'
    systemProperty 'java.io.tmpdir', gwtGlobalCache.getAbsolutePath()
}

task createDraftBundleZip (type: Zip) {
    description 'Creates the Math Apps Bundle file.'
    destinationDir = file(warDirRel)
    baseName = "geogebra-bundle"
    from compileGwt {
        include ("web3d/**")
        into "GeoGebra/HTML5/5.0"
    }
}

// https://issues.gradle.org/browse/GRADLE-2778
// http://discuss.gradle.org/t/javadoc-generation-failed-with-vaadin-dependency/2502/12
javadoc {
    options.addStringOption("sourcepath", "")
}

test {
    ignoreFailures = true
}

spotbugs {
    ignoreFailures = true
    excludeFilter = file("config/findbugs.xml")
}

pmd {
     ignoreFailures = true
     ruleSets = []
     ruleSetFiles = files("config/pmd.xml")
}

cpd {
    ignoreFailures = true
    minimumTokenCount = 100
}

tasks.withType(de.aaschmid.gradle.plugins.cpd.Cpd) {
    exclude "**/*.html"
    exclude "**/*.jj"
    exclude "**/*.xml"
    exclude "**/*.svg"
}
